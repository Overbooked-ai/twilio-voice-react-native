buildscript {
  ext.versions = [
    'java'               : JavaVersion.VERSION_11,
    'androidGradlePlugin': '7.4.2',
    'googleServices'     : '4.3.10',
    'voiceAndroid'       : '6.7.1',
    'androidxCore'       : '1.10.1',
    'androidxLifecycle'  : '2.2.0',
    'audioSwitch'        : '1.1.8',
    'firebaseMessaging'  : '23.4.0',
    'kotlin'             : '1.9.24'
  ]
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
    }

    dependencies {
      classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
      classpath "com.google.gms:google-services:${versions.googleServices}"
      classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
  }
}

apply plugin: 'com.android.library'
apply plugin: "kotlin-android"

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  namespace 'com.twiliovoicereactnative'
  compileSdk safeExtGet('TwilioVoiceReactNative_compileSdkVersion', 34)
  defaultConfig {
    minSdkVersion safeExtGet('TwilioVoiceReactNative_minSdkVersion', 23)
    targetSdkVersion safeExtGet('TwilioVoiceReactNative_targetSdkVersion', 34)
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility versions.java
    targetCompatibility versions.java
  }
  buildFeatures {
    // for buildconfig construction
    buildConfig true
  }
}

repositories {
  google()
  mavenCentral()
  maven {
    url 'https://maven.google.com/'
    name 'Google'
  }
}

dependencies {
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"  // From node_modules
  implementation "com.twilio:voice-android:${versions.voiceAndroid}"
  implementation "androidx.core:core:${versions.androidxCore}"
  implementation "androidx.lifecycle:lifecycle-extensions:${versions.androidxLifecycle}"
  implementation "com.google.firebase:firebase-messaging:${versions.firebaseMessaging}"
  implementation "com.twilio:audioswitch:${versions.audioSwitch}"
  implementation 'com.google.android.material:material:1.1.0'

  constraints {
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
      because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
    }
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
      because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
    }
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.0") {
      because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
    }
  }
}

// Ensure resource packaging task depends on resource value generation task for all variants
// Use tasks.matching for potentially later configuration phase execution
android.libraryVariants.all { variant ->
    def generateTaskName = "generate${variant.name.capitalize()}ResValues"
    // Corrected target task name based on common AGP patterns (check build logs if this is wrong)
    def processTaskName = "process${variant.name.capitalize()}JavaRes" 
    // Alternative if above is wrong: def processTaskName = "package${variant.name.capitalize()}Resources"
    // Alternative if above is wrong: def processTaskName = "process${variant.name.capitalize()}Resources" 

    tasks.matching { it.name == processTaskName }.configureEach { processTask ->
        def generateTask = tasks.findByName(generateTaskName)
        if (generateTask != null) {
            processTask.dependsOn(generateTask)
            // Use logger.quiet or logger.info for build logs
            project.logger.quiet("AGP Task Dependency Fix: Task '${processTask.name}' now dependsOn '${generateTask.name}'")
        } else {
             project.logger.warn("AGP Task Dependency Fix: Could not find task '${generateTaskName}' to make '${processTask.name}' depend on.")
        }
    }
}
